


object AK4jets : JET
  select {JET_}Pt > 30
  select {JET_}AbsEta < 2.4

object AK8jets : FJET
  select {FJET_}Pt > 200
  select {FJET_}Eta < 2.4

object bjetsLoose : AK4jets
  select {AK4jets_}btagDeepB > 0.152

object bjetsMedium : AK4jets
  select {AK4jets_ }btagDeepB > 0.4941

object bjetsTight : AK4jets
  select {AK4jets_}btagDeepB > 0.8001

object WjetsMasstag : AK8jets
  select {AK8jets_}msoftdrop [] 65 105

object Wjets : WjetsMasstag
  select {WjetsMasstag_}tau2 / {WjetsMasstag_}tau1 <= 0.4

object WjetsAntitag : WjetsMasstag
  select {WjetsMasstag_}tau2 / {WjetsMasstag_}tau1 > 0.4

object topjetsMasstag : AK8jets
  select {AK8jets_}Pt > 400
  select {AK8jets_}msoftdrop [] 105 210

object topjetsMasstag0b : topjetsMasstag
  select {topjetsMasstag_ }btagDeepB < 0.1522

object topjets : topjetsMasstag
  select {topjetsMasstag_}btagDeepB >= 0.1522
  select {topjetsMasstag_}tau3 / {topjetsMasstag_}tau2 < 0.46   ##########put this into FATJET

object topjetsAntitag : topjetsMasstag
  select {topjetsMasstag_}btagDeepB < 0.1522
  select {topjetsMasstag_}tau3 / {topjetsMasstag_}tau2 >= 0.46

object muonsVeto : MUO
  select {MUO_}Pt > 5
  select {MUO_}AbsEta < 2.4
  select {MUO_}softId == 1
  select {MUO_}miniPFRelIsoAll < 0.2
  select abs({MUO_}dxy) < 0.2
  select abs({MUO_}dz) < 0.5

object muonsSel : MUO
  select {MUO_}Pt > 10
  select {MUO_}AbsEta < 2.4
  select {MUO_}miniPFRelIsoAll < 0.15
  select abs({MUO_}dxy) < 0.05
  select abs({MUO_}dz) < 0.1

object electronsVeto : ELE
  select {ELE_}Pt > 5
  select {ELE_}AbsEta < 2.5
  select {ELE_}miniPFRelIsoAll < 0.1
  select abs({ELE_}dxy) < 0.05
  select abs({ELE_}dz) < 0.1

object electronsSel : ELE
  select {ELE_}Pt > 10
  select {ELE_}AbsEta < 2.5
  select {ELE_}AbsEta  ][ 1.442 1.556
  select {ELE_}miniPFRelIsoAll < 0.1
  select abs({ELE_}dxy) < 0.05
  select abs({ELE_}dz) < 0.1

object tausVeto : TAU
  select {Tau_}Pt > 18
  select {Tau_}AbsEta < 2.5
  select {Tau_}dMVAnewDM2017v2 >= 4

object photons : PHO
  select {PHO_}Pt > 80
  select {PHO_}AbsEta < 2.5

object AK4jetsNopho : AK4jets
  select dR(AK4jets_, photons_ ) >=0.4 OR {photons_}Pt/{AK4jets_}Pt ][ 0.5 2.0

object megajets : AK4jets
  select fmegajets(AK4jets) == 2

object megajetsNopho : AK4jetsNopho
  select fmegajets(AK4jetsNopho) == 2

define MR : fMR(megajets)
define Rsq : (fMTR(megajets, MET) / MR )^0.5
define dphimegajets : dPhi(megajets[0], megajets[1])
define dphimegajetsNopho : dPhi(megajetsNopho[0], megajetsNopho[1])

define METLVe   : METLV_0 + electronsVeto_0
define METLVm   : METLV_0 + muonsVeto_0
define METLVee  : METLV_0 + electronsVeto_0 + electronsVeto_1
define METLVmm  : METLV_0 + muonsVeto_0 + muonsVeto_1
define METLVpho : METLV_0 + photons[0]

define Rsqe : (fMTR(megajets, METLVe) / MR)^0.5
define Rsqm : (fMTR(megajets, METLVm) / MR)^0.5
define Rsqee : sqrt(fMTR(megajets, METLVee) / MR)
define Rsqmm : sqrt(fMTR(megajets, METLVmm) / MR)
define MRNopho : fMR(megajetsNopho)
define Rsqpho : sqrt(fMTR(megajetsNopho, METLVpho) / MRNopho)
define MTe : sqrt( 2*{electronsVeto_0}Pt *MET*(1-cos( {METLV_0}Phi - {electronsVeto_0}Phi )))
define MTm : sqrt( 2*{muonsVeto_0}Pt *MET*(1-cos( {METLV_0}Phi - {muonsVeto_0}Phi )))
define mZ : 91.187


algo TopcategoryCRZ
  select Size(AK4jets) >= 3
  select Size(AK8jets) >= 1
  select Size(megajets) == 2
  select MR > 800
  select (Size(muonsSel) == 2 AND Size(electronsVeto) == 0) OR (Size(electronsSel) == 2 AND Size(muonsVeto) == 0)
  select Size(muonsSel) == 2 ? {muonsSel_0}q + {muonsSel_1}q == 0 : {electronsSel_0}q + {electronsSel_1}q == 0
  select Size(muonsSel) == 2 ? Abs({muonsSel_0}m + {muonsSel_1}m - mZ) < 10 : Abs({electronsSel_0}m + {electronsSel_1}m -  mZ) < 10
  select Size(muonsSel) == 2 ? Rsqmm > 0.08 : Rsqee > 0.08
  select Size(topjetsMasstag) >= 1
  select dphimegajets < 2.8

